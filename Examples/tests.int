type list<'a> = Nil of unit
              | Cons of 'a * box<list<'a>>

/* interaction strategy for tail recursion */
fn tailrecimp(x) =
  case x of
    Inr(a, ()) -> return Inl((), Inr(a, ()))
  | Inl(u, s) ->
      case s of
         Inr(b) -> return Inr(b)
       | Inl(d, (a, ())) -> return Inl((), Inr(a, ()))

/* The tail recursion combinator is implemented by the above strategy */
let tailrec =
   direct(tailrecimp : (('a -> ['b]) -> 'a -> ['b]) -> 'a -> ['b])


/* factorial with accumulator */
let facaux = tailrec (λ facaux ->
    fn (i, acc) ->
      let b = inteq(i, 0) in
      if b then return acc else
        let i' = intsub(i, 1) in
	let acc' = intmul(acc, i) in
        facaux (i', acc')
)

/* factorial */
fn fac(i) =
   facaux (i, 1)

/* factorial test */
fn main1() =
   let v = fac 10 in
     print v

/* stategy implementing the fixed point combinator */
let fiximpl =
 fn x ->
  case x of
   Inl(l, x) ->
       (case x of
          Inl(b, q) ->
	    /* allocate new cons cell */
	    let a = alloc() in
	    /* store Cons(b, l) in cell a */
	    let () = store(a, Cons(b, l)) in
    	    return Inl(a, Inr(q))
        | Inr(a) ->
	    /* load u from cell l */
	    let u = load(l) in
	    /* free cell l */
	    let () = free(l) in
            case u of
               Nil -> return Inr(a)
             | Cons(hd, tl) -> return Inl(tl, Inl(hd, a)))
 | Inr(q) ->
    let a = alloc() in
    let () = store(a, Nil) in
    return Inl(a, Inr(q))

/* fixed point combinator */
let fix = direct(fiximpl: /*{box<list<'b>>}*/ ( /*{'b}*/(''a) -> ''a) -> ''a)

/* fibonacci */
let fib = fix (λ fib ->
   copy fib as fib1, fib2 in
   fn i ->
      let t = intslt(i, 2) in
      if t then
        return 1
      else
	let f1 =
            let i1 = intsub(i, 1) in
	    fib1 i1 in
	let f2 =
            let i2 = intsub(i, 2) in
	    fib2 i2 in
	let s = intadd(f1, f2) in
	return s
   )

let main =
  let v = fib 38 in
  print v
