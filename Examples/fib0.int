type list<'a> = Nil of unit | Cons of 'a * box<list<'a>>

/* stategy implementing the fixed point combinator */
let fiximpl =
 fn x ->
  case x of
   Inl(l, x) ->
       (case x of
          Inl(b, q) ->
	    /* allocate new cons cell */
	    let a = alloc() in
	    /* store Cons(b, l) in cell a */
	    let () = store(a, Cons(b, l)) in
    	    return Inl(a, Inr(q))
        | Inr(a) ->
	    /* load u from cell l */
	    let u = load(l) in
	    /* free cell l */
	    let () = free(l) in
            case u of
               Nil -> return Inr(a)
             | Cons(hd, tl) -> return Inl(tl, Inl(hd, a)))
 | Inr(q) ->
    let a = alloc() in
    let () = store(a, Nil) in
    return Inl(a, Inr(q))

/* fixed point combinator */
let fix = direct(fiximpl: /*{box<list<'b>>}*/ ( /*{'b}*/(''a) -> ''a) -> ''a)

let fibs = fix (Î» fib ->
   copy fib as fib1, fib2 in
   fn i ->
     let b = intslt(i, 2) in
     if b then return 1
     else
       let f1 = let i' = intsub(i,1) in fib1 i' in
       let f2 = let i' = intsub(i,2) in fib2 i' in
       intadd(f1, f2)
   )

let main =
  let v = fibs 37 in
  print v
